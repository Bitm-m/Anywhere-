apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'sdk-editor'
apply plugin: 'com.catchingnow.robfuscate'
apply plugin: 'io.michaelrocks.paranoid'
apply from: 'and_res_guard.gradle'

android {
    signingConfigs {
        release {
            storeFile file('android-key.jks')
            storePassword System.getenv("KSTOREPWD")
            keyAlias = System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion "22.0.7026061"

    defaultConfig {
        applicationId "com.absinthe.anywhere_"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2010900
        versionName "2.1.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [appName: "Anywhere-"]
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "Anywhere-Î²"]
            buildConfigField("boolean", "BETA", 'true')
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "BETA", 'false')
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = variant.versionName
            outputFileName = "Anywhere-${versionName}.apk"
        }
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(name: 'color-picker', ext: 'aar')

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    implementation 'com.absinthe.libraries.me:me:1.0.6'
    implementation 'com.absinthe.libraries.utils:utils:1.1.1'

    def appCenterSdkVersion = '3.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    //Android X
    def room_version = '2.2.6'
    implementation "androidx.room:room-runtime:${room_version}"
    implementation "androidx.room:room-ktx:${room_version}"
    kapt "androidx.room:room-compiler:${room_version}"
    androidTestImplementation "androidx.room:room-testing:${room_version}"

    def lifecycle_version = '2.3.0-rc01'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:${lifecycle_version}"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'

    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.collection:collection-ktx:1.1.0'
    implementation 'androidx.activity:activity-ktx:1.2.0-rc01'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'androidx.annotation:annotation:1.2.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

    //Google
    implementation 'com.google.android.material:material:1.3.0-beta01'

    //Function
    implementation 'moe.shizuku.privilege:api:4.0.0'
    implementation 'me.weishu:free_reflection:3.0.1'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.catchingnow.icebox:SDK:1.0.5'
    implementation 'com.jonathanfinerty.once:once:1.3.0'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.blankj:utilcodex:1.30.4'
    implementation 'com.tencent:mmkv-static:1.2.4'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'
    implementation 'com.github.heruoxin.Delegated-Scopes-Manager:client:master-SNAPSHOT'
    implementation 'com.thegrizzlylabs.sardine-android:sardine-android:0.7'
    implementation "com.github.topjohnwu.libsu:core:3.0.2"

    //UX
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.drakeet.about:about-extension:2.4.1'
    implementation 'com.drakeet.multitype:multitype:4.2.0'
    implementation "com.leinardi.android:speed-dial:3.1.1"
    implementation 'com.github.sephiroth74:android-target-tooltip:2.0.4'

    def rikkaPreference = "4.2.0-alpha03"
    implementation("moe.shizuku.preference:preference-appcompat:$rikkaPreference")
    implementation("moe.shizuku.preference:preference-simplemenu-appcompat:$rikkaPreference")

    //Network
    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation("com.squareup.okio:okio:2.9.0")

    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'

    //Debug
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    testImplementation 'junit:junit:4.13.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'kotlin-android-extensions'
