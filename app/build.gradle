apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'sdk-editor'
apply plugin: 'com.catchingnow.robfuscate'
apply plugin: 'io.michaelrocks.paranoid'
apply plugin: 'com.google.firebase.firebase-perf'
apply from: 'and_res_guard.gradle'

android {
    signingConfigs {
        release {
            storeFile file('android-key.jks')
            storePassword 'yinzhua'
            keyAlias = 'absinthe-android'
            keyPassword 'yinzhua'
        }
    }

    compileSdkVersion 29
    //noinspection GradleDependency
    buildToolsVersion "29.0.2"
    ndkVersion "21.0.6113669"

    defaultConfig {
        applicationId "com.absinthe.anywhere_"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1812
        versionName "1.8.1"
       
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [appName: "Anywhere-"]
        externalNativeBuild {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    android {
        viewBinding.enabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "Anywhere-Î²"]
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        dev {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = variant.versionName
            outputFileName = "Anywhere-${versionName}.apk"
        }
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

    def build_param = "${build}"
    if (build_param != 'dev') {
        //exclude production build
        android.variantFilter { variant ->
            if (variant.buildType.name == 'dev') {
                variant.setIgnore(true)
            }
        }
    } else {
        //exclude all except production build
        android.variantFilter { variant ->
            if (variant.buildType.name != 'dev') {
                variant.setIgnore(true)
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(name: 'color-picker', ext: 'aar')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    //Android X
    def lifecycle_version = '2.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment:1.2.3'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation "androidx.preference:preference:1.1.0"
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-livedata:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"

    //Google
    implementation 'com.google.android.material:material:1.2.0-alpha05'
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-perf:19.0.5'

    //Function
    implementation 'moe.shizuku.privilege:api:3.1.0-alpha4'
    implementation 'me.weishu:free_reflection:2.2.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.catchingnow.icebox:SDK:1.0.5'
//    implementation 'com.github.heruoxin.Delegated-Scopes-Manager:manager:master-SNAPSHOT'
    implementation 'com.jonathanfinerty.once:once:1.3.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.1'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'moe.feng:AlipayZeroSdk:1.1'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation 'com.blankj:utilcodex:1.26.0'

    //UX
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.drakeet.about:about-extension:2.4.1'
    implementation 'com.drakeet.multitype:multitype:4.2.0'
    implementation "com.leinardi.android:speed-dial:3.1.1"
    implementation 'com.github.sephiroth74:android-target-tooltip:2.0.4'

    //Network
    implementation("com.squareup.okhttp3:okhttp:4.3.1")
    implementation("com.squareup.retrofit2:retrofit:2.8.1")
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation("com.squareup.okio:okio:2.5.0")

    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'

    //Debug
    kapt 'androidx.room:room-compiler:2.2.5'
    testImplementation 'junit:junit:4.13'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    androidTestImplementation 'androidx.room:room-testing:2.2.5'
}

apply plugin: 'com.google.gms.google-services'
